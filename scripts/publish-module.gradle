apply plugin: 'maven-publish'
apply plugin: 'signing'

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'BottomSheetDialog wrapper for Jetpack Compose'
                    url = 'https://github.com/workspace/bottomsheetdialog-compose'
                    licenses {
                        license {
                            name = 'Apache-2.0 license'
                            url = 'https://github.com/workspace/bottomsheetdialog-compose/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'workspace'
                            name = 'Kimin Ryu'
                            email = 'ryukimin.workspace@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/workspace/bottomsheetdialog-compose.git'
                        developerConnection = 'scm:git:ssh://github.com/workspace/bottomsheetdialog-compose.git'
                        url = 'https://github.com/workspace/bottomsheetdialog-compose/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
